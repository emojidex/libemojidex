CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

PROJECT(LIBEMOJIDEX)


# Generate version header
SET(EMOJIDEX_LIB_VERSION_MAJOR 0)
SET(EMOJIDEX_LIB_VERSION_MINOR 1)
SET(EMOJIDEX_LIB_VERSION_PATCH 0)
SET(EMOJIDEX_LIB_VERSION "${EMOJIDEX_LIB_VERSION_MAJOR}.${EMOJIDEX_LIB_VERSION_MINOR}.${EMOJIDEX_LIB_VERSION_PATCH}")
CONFIGURE_FILE(${PROJECT_SOURCE_DIR}/src/version.h.in ${PROJECT_SOURCE_DIR}/src/version.h @ONLY)

# SET(CMAKE_POSITION_INDEPENDENT_CODE ON)


# We're using C++11
if(UNIX)
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -std=c++11")
endif()


# Generate dynamic library
FIND_PACKAGE(Boost REQUIRED
	system
	filesystem
	)
FIND_PACKAGE(OpenSSL)

INCLUDE_DIRECTORIES(${PROJECT_SOURCE_DIR}/src)

SET(EMOJIDEX_DYNAMIC_LIB_NAME emojidex)
SET(EMOJIDEX_CPP_FILES
	${PROJECT_SOURCE_DIR}/src/client.cpp
	${PROJECT_SOURCE_DIR}/src/client/transactor.cpp
	${PROJECT_SOURCE_DIR}/src/client/search.cpp
)
SET(EMOJIDEX_LINK_LIBRARIES
	${Boost_FILESYSTEM_LIBRARY}
	${Boost_SYSTEM_LIBRARY}
	${OPENSSL_LIBRARIES}
)

ADD_LIBRARY(${EMOJIDEX_DYNAMIC_LIB_NAME} SHARED ${EMOJIDEX_CPP_FILES})
TARGET_LINK_LIBRARIES(${EMOJIDEX_DYNAMIC_LIB_NAME} ${EMOJIDEX_LINK_LIBRARIES})

ADD_SUBDIRECTORY(${PROJECT_SOURCE_DIR}/spec)


# Installation
INSTALL(FILES 
	${PROJECT_SOURCE_DIR}/src/version.h
	${PROJECT_SOURCE_DIR}/src/client.h
	${PROJECT_SOURCE_DIR}/src/client/search.h
	DESTINATION "include/emojidex"
	)
INSTALL(TARGETS ${EMOJIDEX_DYNAMIC_LIB_NAME} DESTINATION "lib")


# Generate static library for SWIG
SET(STATIC_LIB_TARGET static)
SET(STATIC_LIB_NAME emojidex_static)

ADD_LIBRARY(${STATIC_LIB_TARGET} ${EMOJIDEX_CPP_FILES})
TARGET_LINK_LIBRARIES(${STATIC_LIB_TARGET} ${EMOJIDEX_LINK_LIBRARIES})

SET_TARGET_PROPERTIES(${STATIC_LIB_TARGET}
	PROPERTIES
	EXCLUDE_FROM_ALL TRUE
	POSITION_INDEPENDENT_CODE TRUE
	OUTPUT_NAME ${STATIC_LIB_NAME}
)


# Generate java sources and library with SWIG
FIND_PACKAGE(Java REQUIRED COMPONENTS Runtime Development)
FIND_PACKAGE(JNI REQUIRED)
FIND_PACKAGE(SWIG REQUIRED)
INCLUDE_DIRECTORIES(${JNI_INCLUDE_DIRS})
INCLUDE(UseJava)
INCLUDE(UseSWIG)

SET(CMAKE_JNI_TARGET TRUE)
SET(CMAKE_SWIG_FLAGS -package libemojidex)
SET(CMAKE_SWIG_OUTDIR "src")
SET_SOURCE_FILES_PROPERTIES("${PROJECT_SOURCE_DIR}/src/libemojidex.i"
	PROPERTIES
	CPLUSPLUS 1
	SWIG_FLAGS "-includeall"
)

SET(SWIG_JAVA_TARGET java)
SET(SWIG_JAVA_NAME emojidex-java)
SET(EMOJIDEX_JAVA_FILES
	${PROJECT_BINARY_DIR}/src/Client.java
	${PROJECT_BINARY_DIR}/src/libemojidex.java
	${PROJECT_BINARY_DIR}/src/libemojidexJNI.java
	${PROJECT_BINARY_DIR}/src/SWIGTYPE_p_Emojidex__SearchClient.java
	${PROJECT_SOURCE_DIR}/src/java/NativeLibLoader.java
)

SWIG_ADD_MODULE(${SWIG_JAVA_TARGET} java "src/libemojidex.i")
SWIG_LINK_LIBRARIES(${SWIG_JAVA_TARGET} ${STATIC_LIB_TARGET})
SET_TARGET_PROPERTIES(${SWIG_JAVA_TARGET}
	PROPERTIES
	EXCLUDE_FROM_ALL TRUE
	OUTPUT_NAME ${SWIG_JAVA_NAME}
)

ADD_CUSTOM_COMMAND(TARGET ${SWIG_JAVA_TARGET}
	DEPENDS ${STATIC_LIB_TARGET}
)


# Generate jar with SWIG
SET(SWIG_JAR_TARGET jar)
SET(SWIG_JAR_NAME libemojidex)

ADD_JAR(${SWIG_JAR_TARGET}
	SOURCES ${EMOJIDEX_JAVA_FILES}
	VERSION ${EMOJIDEX_LIB_VERSION}
	OUTPUT_NAME ${SWIG_JAR_NAME}
)
SET_TARGET_PROPERTIES(${SWIG_JAR_TARGET}
	PROPERTIES
	EXCLUDE_FROM_ALL TRUE
)

INSTALL_JAR(${SWIG_JAR_TARGET} ${LIB_INSTALL_DIR}/emojidex)
INSTALL_JNI_SYMLINK(${SWIG_JAR_TARGET} .)

SET(JAR_FILE_NAME ${SWIG_JAR_NAME}-${EMOJIDEX_LIB_VERSION}.jar)
SET(SWIG_LIB_FILE_NAME lib${SWIG_JAVA_NAME}.so)

ADD_CUSTOM_COMMAND(TARGET ${SWIG_JAR_TARGET}
	POST_BUILD
	DEPENDS ${SWIG_JAVA_TARGET}

	COMMAND ${JAVA_ARCHIVE} -uvf ${JAR_FILE_NAME} ${SWIG_LIB_FILE_NAME}
)
